gfx.init(800, 600, "Erkao Flabby Bird");
gfx.fps(60);

let width = 800;
let height = 600;
let groundY = height - 80;

let birdX = 180;
let birdY = height / 2;
let birdRadius = 14;
let birdVel = 0;

let gravity = 900;
let jumpVel = -320;

let pipeWidth = 70;
let pipeGap = 170;
let pipeSpeed = 220;
let spawnInterval = 1.35;
let spawnTimer = 0;

let pipes = [];
let score = 0;
let best = 0;
let state = "ready";

let seed = time.now() * 1000;
seed = seed - math.floor(seed / 233280) * 233280;

fun rand() {
  seed = seed * 9301 + 49297;
  seed = seed - math.floor(seed / 233280) * 233280;
  return seed / 233280;
}

fun randRange(min, max) {
  return min + (max - min) * rand();
}

fun toText(value) {
  return json.stringify(value);
}

fun resetGame() {
  birdY = height / 2;
  birdVel = 0;
  pipes = [];
  spawnTimer = 0;
  score = 0;
}

let lastTime = gfx.time();

while (gfx.poll()) {
  let now = gfx.time();
  let dt = now - lastTime;
  lastTime = now;
  if (dt > 0.05) { dt = 0.05; }

  let flap = gfx.keyPressed("space") or gfx.keyPressed("up") or gfx.keyPressed("w");

  if (state == "ready") {
    if (flap) {
      state = "playing";
      birdVel = jumpVel;
    }
  } else if (state == "playing") {
    if (flap) { birdVel = jumpVel; }

    birdVel = birdVel + gravity * dt;
    birdY = birdY + birdVel * dt;

    spawnTimer = spawnTimer + dt;
    if (spawnTimer >= spawnInterval) {
      spawnTimer = 0;
      let gapY = randRange(140, groundY - 140);
      let pipe = { x: width + 20, gapY: gapY, passed: false };
      push(pipes, pipe);
    }

    let newPipes = [];
    let i = 0;
    while (i < len(pipes)) {
      let pipe = pipes[i];
      pipe["x"] = pipe["x"] - pipeSpeed * dt;

      if (!pipe["passed"] and pipe["x"] + pipeWidth < birdX) {
        pipe["passed"] = true;
        score = score + 1;
        if (score > best) { best = score; }
      }

      let gapTop = pipe["gapY"] - pipeGap / 2;
      let gapBottom = pipe["gapY"] + pipeGap / 2;
      let hitX = birdX + birdRadius > pipe["x"] and birdX - birdRadius < pipe["x"] + pipeWidth;
      if (hitX and (birdY - birdRadius < gapTop or birdY + birdRadius > gapBottom)) {
        state = "gameover";
      }

      if (pipe["x"] + pipeWidth > -20) {
        push(newPipes, pipe);
      }

      i = i + 1;
    }
    pipes = newPipes;

    if (birdY + birdRadius >= groundY or birdY - birdRadius <= 0) {
      state = "gameover";
    }
  } else if (state == "gameover") {
    if (flap) {
      resetGame();
      state = "playing";
      birdVel = jumpVel;
    }
  }

  gfx.clear("skyblue");

  gfx.circle("gold", 680, 90, 35);

  let j = 0;
  while (j < len(pipes)) {
    let pipe = pipes[j];
    let gapTop = pipe["gapY"] - pipeGap / 2;
    let gapBottom = pipe["gapY"] + pipeGap / 2;
    gfx.rect("green", pipe["x"], 0, pipeWidth, gapTop);
    gfx.rect("green", pipe["x"], gapBottom, pipeWidth, groundY - gapBottom);
    gfx.rectLine("darkgreen", pipe["x"], 0, pipeWidth, gapTop);
    gfx.rectLine("darkgreen", pipe["x"], gapBottom, pipeWidth, groundY - gapBottom);
    j = j + 1;
  }

  gfx.rect("darkgreen", 0, groundY, width, height - groundY);
  gfx.rectLine("black", 0, groundY, width, height - groundY);

  gfx.circle("yellow", birdX, birdY, birdRadius);
  gfx.circleLine("orange", birdX, birdY, birdRadius);

  gfx.text("Score: " + toText(score), 12, 10, "white", 20);
  gfx.text("Best: " + toText(best), 12, 34, "white", 16);

  if (state == "ready") {
    gfx.text("Flabby Bird", 270, 160, "white", 34);
    gfx.text("Press SPACE / W / UP to flap", 200, 210, "lightgray", 18);
  } else if (state == "gameover") {
    gfx.text("Game Over", 290, 160, "white", 32);
    gfx.text("Press SPACE to restart", 230, 210, "lightgray", 18);
  }

  gfx.present();
}

gfx.quit();
print("Done.");
